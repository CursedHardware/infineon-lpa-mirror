/*
 * THE SOURCE CODE AND ITS RELATED DOCUMENTATION IS PROVIDED "AS IS". INFINEON
 * TECHNOLOGIES MAKES NO OTHER WARRANTY OF ANY KIND,WHETHER EXPRESS,IMPLIED OR,
 * STATUTORY AND DISCLAIMS ANY AND ALL IMPLIED WARRANTIES OF MERCHANTABILITY,
 * SATISFACTORY QUALITY, NON INFRINGEMENT AND FITNESS FOR A PARTICULAR PURPOSE.
 *
 * THE SOURCE CODE AND DOCUMENTATION MAY INCLUDE ERRORS. INFINEON TECHNOLOGIES
 * RESERVES THE RIGHT TO INCORPORATE MODIFICATIONS TO THE SOURCE CODE IN LATER
 * REVISIONS OF IT, AND TO MAKE IMPROVEMENTS OR CHANGES IN THE DOCUMENTATION OR
 * THE PRODUCTS OR TECHNOLOGIES DESCRIBED THEREIN AT ANY TIME.
 *
 * INFINEON TECHNOLOGIES SHALL NOT BE LIABLE FOR ANY DIRECT, INDIRECT OR
 * CONSEQUENTIAL DAMAGE OR LIABILITY ARISING FROM YOUR USE OF THE SOURCE CODE OR
 * ANY DOCUMENTATION, INCLUDING BUT NOT LIMITED TO, LOST REVENUES, DATA OR
 * PROFITS, DAMAGES OF ANY SPECIAL, INCIDENTAL OR CONSEQUENTIAL NATURE, PUNITIVE
 * DAMAGES, LOSS OF PROPERTY OR LOSS OF PROFITS ARISING OUT OF OR IN CONNECTION
 * WITH THIS AGREEMENT, OR BEING UNUSABLE, EVEN IF ADVISED OF THE POSSIBILITY OR
 * PROBABILITY OF SUCH DAMAGES AND WHETHER A CLAIM FOR SUCH DAMAGE IS BASED UPON
 * WARRANTY, CONTRACT, TORT, NEGLIGENCE OR OTHERWISE.
 *
 * (C)Copyright INFINEON TECHNOLOGIES All rights reserved
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'de.undercouch.download'

android {
    namespace 'com.infineon.esim.lpa'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.infineon.esim.lpa"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 40003
        versionName "4.0.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled = false
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

// Task to print project version name (e.g. for CI/CD purposes)
task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

// Gradle tasks to automatically download Identiv Smartcard SDK dependency
task downloadIdentivAndroidSdkZipFile(type: Download) {
    src 'https://files.identiv.com/products/smart-card-readers/developer-tools/Android_Smartcard_SDK_v1.2.zip'
    dest new File(buildDir, 'resources/libs/com.identiv.android.sdk.1.2.zip')
    overwrite false
}
task resolveIdentivAndroidSdkDependency(dependsOn: downloadIdentivAndroidSdkZipFile, type: Copy) {
    outputs.file('libs/identiv/lib/androidSCardV1.2.jar')
    from zipTree(downloadIdentivAndroidSdkZipFile.dest)
    into "libs/identiv"
}

preBuild.dependsOn resolveIdentivAndroidSdkDependency

dependencies {
    // ---------------------------------------------------------------------------------------------
    // Module dependencies
    // ---------------------------------------------------------------------------------------------
    implementation project(path: ':messages')
    implementation project(path: ':util')
    implementation project(path: ':core')

    // ---------------------------------------------------------------------------------------------
    // Identiv reader library
    // ---------------------------------------------------------------------------------------------
    implementation fileTree(dir: 'libs/identiv/lib', include: ['*.jar'])

    // ---------------------------------------------------------------------------------------------
    // Google libraries
    // ---------------------------------------------------------------------------------------------
    // Open source license plugin (see: https://developers.google.com/android/guides/opensource)
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    // Android Material design
    implementation "com.google.android.material:material:1.8.0"
    // QR Code Scanner
    implementation "com.google.mlkit:barcode-scanning:17.1.0"
    // Location Services
    implementation "com.google.android.gms:play-services-location:21.0.1"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // AndroidX / Jetpack Dependencies (see: https://developer.android.com/jetpack/androidx/versions)
    ////////////////////////////////////////////////////////////////////////////////////////////////

    // ---------------------------------------------------------------------------------------------
    // CameraX library
    // ---------------------------------------------------------------------------------------------
    // Camera2 library
    implementation "androidx.camera:camera-camera2:1.3.0-alpha05"
    // Camera core
    implementation "androidx.camera:camera-core:1.3.0-alpha05"
    // Camera lifecycle management
    implementation "androidx.camera:camera-lifecycle:1.3.0-alpha05"
    // Camera extensions
    implementation "androidx.camera:camera-extensions:1.3.0-alpha05"
    // Camera view fragment
    implementation "androidx.camera:camera-view:1.3.0-alpha05"

    // ---------------------------------------------------------------------------------------------
    // UI support
    // ---------------------------------------------------------------------------------------------
    // AppCompat
    implementation "androidx.appcompat:appcompat:1.6.1"
    // Activity (for permission handling)
    implementation "androidx.activity:activity:1.7.0"
    // Fragment is needed for ActivityResult API to work properly (needs 1.3.0-rc01, 1.2.5 is not supporting ActivityResult)
    // see: https://stackoverflow.com/questions/62771948/new-result-api-error-can-only-use-lower-16-bits-for-requestcode
    implementation "androidx.fragment:fragment:1.5.6"
    // ConstraintLayout for profile list
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    // SwiperefreshLayout for profile list
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // Preference
    implementation "androidx.preference:preference:1.2.0"
    // WorkManager
    implementation "androidx.work:work-runtime:2.8.1"

    // ---------------------------------------------------------------------------------------------
    // Lifecycle management
    // ---------------------------------------------------------------------------------------------
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:2.6.1"
    // Lifecycle runtime only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime:2.6.1"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.1"
    // Annotation processor
    // annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.1"
    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:2.6.1"
    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation "androidx.lifecycle:lifecycle-process:2.6.1"
    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.6.1"
    implementation files('lib/acssmc-1.1.5.jar')

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Testing
    ////////////////////////////////////////////////////////////////////////////////////////////////
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"

    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"

    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Compatibility
    ////////////////////////////////////////////////////////////////////////////////////////////////

    // Fixing Duplicate class kotlin.... issue
    // See: https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}