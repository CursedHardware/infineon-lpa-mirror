apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'de.undercouch.download'

android {
    namespace 'com.infineon.esim.lpa'
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.infineon.esim.lpa"
        minSdkVersion 28
        targetSdkVersion 32
        versionCode 40000
        versionName "4.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'gson-proguard-rules.txt'
        }
        debug {
            minifyEnabled = false
            proguardFiles 'gson-proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        jniLibs {
            excludes += ['libbarhopper_v2.so']
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

/*
 * THE SOURCE CODE AND ITS RELATED DOCUMENTATION IS PROVIDED "AS IS". INFINEON
 * TECHNOLOGIES MAKES NO OTHER WARRANTY OF ANY KIND,WHETHER EXPRESS,IMPLIED OR,
 * STATUTORY AND DISCLAIMS ANY AND ALL IMPLIED WARRANTIES OF MERCHANTABILITY,
 * SATISFACTORY QUALITY, NON INFRINGEMENT AND FITNESS FOR A PARTICULAR PURPOSE.
 *
 * THE SOURCE CODE AND DOCUMENTATION MAY INCLUDE ERRORS. INFINEON TECHNOLOGIES
 * RESERVES THE RIGHT TO INCORPORATE MODIFICATIONS TO THE SOURCE CODE IN LATER
 * REVISIONS OF IT, AND TO MAKE IMPROVEMENTS OR CHANGES IN THE DOCUMENTATION OR
 * THE PRODUCTS OR TECHNOLOGIES DESCRIBED THEREIN AT ANY TIME.
 *
 * INFINEON TECHNOLOGIES SHALL NOT BE LIABLE FOR ANY DIRECT, INDIRECT OR
 * CONSEQUENTIAL DAMAGE OR LIABILITY ARISING FROM YOUR USE OF THE SOURCE CODE OR
 * ANY DOCUMENTATION, INCLUDING BUT NOT LIMITED TO, LOST REVENUES, DATA OR
 * PROFITS, DAMAGES OF ANY SPECIAL, INCIDENTAL OR CONSEQUENTIAL NATURE, PUNITIVE
 * DAMAGES, LOSS OF PROPERTY OR LOSS OF PROFITS ARISING OUT OF OR IN CONNECTION
 * WITH THIS AGREEMENT, OR BEING UNUSABLE, EVEN IF ADVISED OF THE POSSIBILITY OR
 * PROBABILITY OF SUCH DAMAGES AND WHETHER A CLAIM FOR SUCH DAMAGE IS BASED UPON
 * WARRANTY, CONTRACT, TORT, NEGLIGENCE OR OTHERWISE.
 *
 * (C)Copyright INFINEON TECHNOLOGIES All rights reserved
 */

// Gradle tasks to automatically download Identiv Smartcard SDK dependency
task downloadIdentivAndroidSdkZipFile(type: Download) {
    src 'https://files.identiv.com/products/smart-card-readers/developer-tools/Android_Smartcard_SDK_v1.2.zip'
    dest new File(buildDir, 'resources/libs/com.identiv.android.sdk.1.2.zip')
    overwrite false
}
task resolveIdentivAndroidSdkDependency(dependsOn: downloadIdentivAndroidSdkZipFile, type: Copy) {
    outputs.file('libs/identiv/lib/androidSCardV1.2.jar')
    from zipTree(downloadIdentivAndroidSdkZipFile.dest)
    into "/libs/identiv"
}

getDependencies.dependsOn resolveIdentivAndroidSdkDependency

dependencies {
    // ---------------------------------------------------------------------------------------------
    // Module dependencies
    // ---------------------------------------------------------------------------------------------
    implementation project(path: ':messages')
    implementation project(path: ':util')
    implementation project(path: ':core')

    // ---------------------------------------------------------------------------------------------
    // Identiv reader library
    // ---------------------------------------------------------------------------------------------
    implementation fileTree(dir: '/libs/identiv/lib', include: ['*.jar'])

    // ---------------------------------------------------------------------------------------------
    // Google libraries
    // ---------------------------------------------------------------------------------------------
    // Open source license plugin (see: https://developers.google.com/android/guides/opensource)
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    // Android Material design
    implementation "com.google.android.material:material:1.6.1"
    // QR Code Scanner
    implementation "com.google.mlkit:barcode-scanning:17.0.2"
    // Location Services
    implementation "com.google.android.gms:play-services-location:20.0.0"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // AndroidX / Jetpack Dependencies (see: https://developer.android.com/jetpack/androidx/versions)
    ////////////////////////////////////////////////////////////////////////////////////////////////

    // ---------------------------------------------------------------------------------------------
    // CameraX library
    // ---------------------------------------------------------------------------------------------
    def cameraVersion = "1.2.0-alpha02"
    // Camera2 library
    implementation "androidx.camera:camera-camera2:$cameraVersion"
    // Camera core
    implementation "androidx.camera:camera-core:$cameraVersion"
    // Camera lifecycle management
    implementation "androidx.camera:camera-lifecycle:$cameraVersion"
    // Camera extensions
    implementation "androidx.camera:camera-extensions:$cameraVersion"
    // Camera view fragment
    implementation "androidx.camera:camera-view:$cameraVersion"

    // ---------------------------------------------------------------------------------------------
    // UI support
    // ---------------------------------------------------------------------------------------------
    // AppCompat
    implementation "androidx.appcompat:appcompat:1.4.2"
    // Activity (for permission handling)
    implementation "androidx.activity:activity:1.4.0"
    // Fragment is needed for ActivityResult API to work properly (needs 1.3.0-rc01, 1.2.5 is not supporting ActivityResult)
    // see: https://stackoverflow.com/questions/62771948/new-result-api-error-can-only-use-lower-16-bits-for-requestcode
    implementation "androidx.fragment:fragment:1.4.1"
    // ConstraintLayout for profile list
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    // SwiperefreshLayout for profile list
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // Preference
    implementation "androidx.preference:preference:1.2.0"
    // WorkManager
    implementation "androidx.work:work-runtime:2.7.1"

    // ---------------------------------------------------------------------------------------------
    // Lifecycle management
    // ---------------------------------------------------------------------------------------------
    def lifecycle_version = "2.4.1"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    // Lifecycle runtime only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    // Annotation processor
    // annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"

    implementation "androidx.work:work-runtime:2.7.1"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Testing
    ////////////////////////////////////////////////////////////////////////////////////////////////
    testImplementation "androidx.arch.core:core-testing:2.1.0"
//    testImplementation "junit:junit:4.13.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.1"

    androidTestImplementation "androidx.test:rules:1.4.1-alpha07"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    def espressoVersion = "3.5.0-alpha07"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Compatibility
    ////////////////////////////////////////////////////////////////////////////////////////////////
    // Fix "warning: unknown enum constant ... " during build
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0"
}